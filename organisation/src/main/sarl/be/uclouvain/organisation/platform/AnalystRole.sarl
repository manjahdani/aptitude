package be.uclouvain.organisation.platform

import be.uclouvain.organisation.AuthorizationToPerformMission
import be.uclouvain.organisation.PlatformOrganisationInfo
import be.uclouvain.organisation.SignalID
import be.uclouvain.organisation.TOLDOrganisationInfo
import be.uclouvain.organisation.platform.util.MissionData
import be.uclouvain.organisation.told.util.AlgorithmInfo
import io.sarl.core.Behaviors
import io.sarl.core.ExternalContextAccess
import io.sarl.core.Initialize
import io.sarl.core.Logging
import io.sarl.core.OpenEventSpace
import io.sarl.lang.core.AgentContext
import java.util.UUID

/** 
 * 
 * The Analyst gathers information about a mission and collects Events of Interest or more generally
 * intelligence (e.g. statistics) for the Client. The role could recommend actions as well. The concept
 * is not necessarily holonic.
 * 
 * As an example, an Analyst received 175 events of incidents involving bikes and cars from one
 * Algorithm. After analysing the incidents, it could draw recommendations to the Client to take
 * actions such as a preventive campaign or new installations.
 * 
 * We advise Analysts specializing to one scenario; consequently, you need two Analysts to perform the counting of vehicles and detect traffic offenses. 
 * However, those Analysts could use the same Observer if they rely on those perceptions. 
 * Therefore, we advise an Analyst rely on one and only one Observer (itself composed of other Observers). 
 * 
 * @author $Author: manjahdani$
 * @version $0.0.2$
 * @date $16/04/2021$
 * @mavengroupid $be.uclouvain.aptitude$
 * @mavenartifactid $organisation$
 */
behavior AnalystRole {
	
	uses Logging, Behaviors, ExternalContextAccess
	
	protected var platformContext : AgentContext // Context of local platform
	protected var privatePlatformSpace : OpenEventSpace // Private channel between the agent and local Platform
	
	protected var TOLDContext : AgentContext // Context of local TOLD
	protected var privateTOLDSpace : OpenEventSpace // Private channel between the agent and local TOLD
	
	protected var missionData : MissionData
	protected var missionSpace : OpenEventSpace

	var providerID : UUID // agent transmitting the information
	
	/** 
	 * Receiving this event, the behavior has to update its fields of perception.
	 */
	val availableObservers = newArrayList("careful", "dynamic", "dynamic ","balanced")
	
	
	@SuppressWarnings("potential_field_synchronization_problem")
	on Initialize {
		missionData = occurrence.parameters.get(0) as MissionData
		debug("starts mission of vehicle counting with sensitivity:  " + availableObservers.get(missionData.sensitivity))
	}
	
	@SuppressWarnings("potential_field_synchronization_problem")
	
	// @FIXME There are global behavior organisational Behavior
	on PlatformOrganisationInfo {
		debug("Joined platform organisation: " + occurrence.privateCommunicationChannel + " (" + occurrence.context +
			").")
		if (occurrence.context.ID == missionData.platformID) {
			debug("sends add mission to - " + occurrence.context.ID.toString.substring(0,5))
			platformContext = occurrence.context
			privatePlatformSpace = occurrence.privateCommunicationChannel
			privatePlatformSpace.registerStrongParticipant(asEventListener)
			info("starts mission with spaceID - " + missionData.missionID)
			emit(privatePlatformSpace,new Request2PerformMission(missionData))[it.ID == missionData.platformID] // @FIXME AddMission 
			debug("asks for algorithm performing vehicle counting to platform")
		}
	}
	@SuppressWarnings("potential_field_synchronization_problem")
	on AuthorizationToPerformMission {
		debug("Received authorisation to perform a mission within the space : " + occurrence.missionSpace.spaceID.ID)
		missionSpace = occurrence.missionSpace;
		missionSpace.registerStrongParticipant(asEventListener)
		//platformSpace.emit(new LocalDatabaseRequest(defaultContext))[it.ID == platformContext.ID]
		emit(privatePlatformSpace,new AddObserver(new AlgorithmInfo("APTITUDE", "COUNTER"), new AlgorithmInfo("EXPERT", "ROADANALYST")))
	}

	on SignalID {
		
		debug("received the provider ID \n sending the missionSpace")
		
		emit(platformContext.defaultSpace, new AddMission(missionSpace)) [
			it.ID == occurrence.signalID
		] 
	}
	@SuppressWarnings("potential_field_synchronization_problem")
	on TOLDOrganisationInfo {
		//info("Joined TOLD organisation: " + occurrence.privateCommunicationChannel + " (" + occurrence.context + ").")
		TOLDContext = occurrence.context
		privateTOLDSpace = occurrence.privateCommunicationChannel
		privateTOLDSpace.registerStrongParticipant(asEventListener)
	}
	@SuppressWarnings("potential_field_synchronization_problem")
	on HyperParametersRequest {
		info("received a request for mission parameters \n will send the user sensitivity : " + availableObservers.get(missionData.sensitivity))
		providerID=occurrence.source.ID
		emit(
			platformContext.defaultSpace,
			new ProcessingHyperParameters(
				missionData.sensitivity,
				missionData.optimalSearchEnabled))[it.ID==providerID]
	}
}
