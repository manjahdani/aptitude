package be.uclouvain.organisation.told

import be.uclouvain.organisation.OrganizationalRole
import be.uclouvain.organisation.events.AddMember
import be.uclouvain.organisation.events.AuthorizationToJoinContext
import be.uclouvain.organisation.platform.PlatformOrganizationInfo
import be.uclouvain.organisation.told.events.AddEntry
import be.uclouvain.organisation.told.events.DataEntry
import be.uclouvain.organisation.told.events.DeleteEntry
import be.uclouvain.organisation.told.events.QueryAnswer
import be.uclouvain.organisation.told.events.ReadEntry
import io.sarl.core.Behaviors
import io.sarl.core.ExternalContextAccess
import io.sarl.core.InnerContextAccess
import io.sarl.core.Logging
import java.util.UUID

/** 
 * Trusted Observations and Learning Database is a database aiming to achieve two goals. 
 * 
 *  - Storing the information communicated by a platform.  
 * 
 *  - Contains ground truth signals dedicated to learning.
 * 
 * @author $Author: manjahdani$
 * @version $0.0.2$
 * @date $16/04/2021$
 * @mavengroupid $be.uclouvain.aptitude$
 * @mavenartifactid $organisation$
 * 
 */
behavior TOLDRole extends OrganizationalRole {
	
	uses Behaviors, AccessDatabaseCapacity, InnerContextAccess,Logging, ExternalContextAccess
	
	
	protected var platformID : UUID

	/*
	 * The platform registerStrongParticipant the context of a platform 
	 */

	@SuppressWarnings("discouraged_occurrence_readonly_use")
	on AddEntry{
		debug("creating entry for " + occurrence.key.toString.substring(0,5))
		create(occurrence.key, occurrence.data)
	}
	on DeleteEntry{
		delete(occurrence.key)
	}
	@SuppressWarnings("discouraged_occurrence_readonly_use")
	on DataEntry {
		//info("Updating key : " + occurrence.key  +" - " + occurrence.class)
		update(occurrence.key, occurrence.data)
	}
	
	/*
	 * Send if available the stored data about it. 
	 */
	@SuppressWarnings("discouraged_occurrence_readonly_use")
	
	on AddMember{
		debug("received add member from " + occurrence.source.ID.toString.substring(0, 5) + "to invite within my context " + occurrence.memberID)
		
		emit(occurrence.communicationChannel,
			new AuthorizationToJoinContext(this.ID, innerContext.defaultSpace.spaceID.ID))[it.ID==occurrence.memberID]
	}
	@SuppressWarnings("discouraged_occurrence_readonly_use")
	on ReadEntry {
		debug("Reading entry and sending -" + read(occurrence.key))
		emit(privateSpacesListeners.get(occurrence.source.ID), new QueryAnswer(read(occurrence.key)))
	}


	on PlatformOrganizationInfo {
		debug("Platform organisation - " + occurrence.platformName + " encountered ")
		platformID = occurrence.source.ID
		if (!privateSpacesListeners.containsKey(platformID)){
			info("Platform was not encountered")
				val privateChannel = occurrence.privateCommunicationChannel
				privateSpacesListeners.put(platformID, privateChannel)
				privateChannel.registerStrongParticipant(asEventListener)
				emit(privateChannel, getOrganizationInfo(platformID))[it.ID == platformID]
		}	
	}
	def getOrganizationInfo(memberID : UUID) : TOLDOrganizationInfo{
		
		return new TOLDOrganizationInfo(innerContext, privateSpacesListeners.get(memberID), read(memberID))
	}
}
